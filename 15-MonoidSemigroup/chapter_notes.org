* 15.1 Monoids and semigroups

- Haskell community looks for abstract patterns
  - With well defined, lawful representations inmathematics

This chapter will include discussions of:

- Algebras
- Laws
- Monoids
- Semigroups

* 15.2 What we talk about when we talk about algebras

- Algebra (Mathematics) :: The study of mathematical symbols and the
     rules governing their manipulation.
  - differentiated from arithmetic by its use of abstractions such as
    variables.

- An Algebra :: some operations and the set they operate over.

In algebra, we care less about the values, and more about the rules of
their use.

In Haskell, algebras can be implemented with typeclasses.
  - The /typeclass/ defines the operations.
  - The set being operated on is the /type/ the operations are
    for. (Remember, types are like sets).

In Haskell, one of the algebras we work with is ~Monoid~.

* 15.3 Monoid

- Monoid :: A binary associative operation with an identity.

* 15.4 How Monoid is defined in Haskell

- Monoid is defined as a typeclass
- We think of types as /having/ an instance of a typeclass.

The ~Monoid~ typeclass is defined:

#+BEGIN_SRC haskell
class Monoid m where
  mempty :: m
  mappend :: m -> m -> m
  mconcat :: [m] -> m
  mconcat = foldr mappend mempty
#+END_SRC

In the above

- mempty is the identity value
- mappend is the binary associative operator

* 15.5 Examples of using Monoid

Monoids already exist all over the place. We can list a bunch of
examples.

** List

We have ~mappend~ defined:

    : ghci> mappend [1, 2, 3] [4, 5, 6]
    : [1, 2, 3, 4, 5, 6]

And ~mempty~

    : ghci> (mempty :: [Int])
    : []

Seeing the above, it's clear how the defintion of ~Monoid~ for lists
should work.

#+BEGIN_SRC haskell
instance Monoid [a] where
  mempty = []
  mappend = (++)
#+END_SRC

* 15.6 Why integer doesn't have a Monoid

- The type ~Integer~ doesn't have a monoid instance
- Neither do any numeric types
- Even though they have monoidal operations

The reason is because either addition or multiplication could be used
as the monoid. This would make the definition/behavior of ~mappend~
and ~mempty~ unclear.

** Sum and Product

To avoid having to make a choice, the ~Monoid~ library defines the
~Sum~ and ~Product~ newtypes around the numeric types. These newtypes
use addition and multiplication for their monoid instances
respectively.

- *Remember* ~newtype~ defintions are fundamentally the underlying
  type, but allow for differing typeclass implementations

We can see this in action:

    : ghci> import Data.Monoid
    : ghci> mappend (Sum 1) (Sum 5)
    : Sum {getSum = 6}
    : ghci> mappend (Product 2.5) (Product 2.0)
    : Product {getProduct = 5.0}

Note that both integral and floating point work. The ~Product~ and
~Sum~ newtypes apply to all types with ~Num~ instances.

There's an infix version of ~mappend~:

    : ghci> "One " <> "Two " <> "Three"
    : "One Two Three"

It makes sense for there to be an infix function considering the
operator is an associative binary operator.

* 15.7 Why bother?

- Monoids are really common
- Useful abstraction
- Principled laws let you know when you can combine monoidal
  operations safely.

Some examples of useful things that follow monoidal rules

- An api that incrementally processes a large dataset
- guarantees needed to roll up aggregations in a parallel concurrent
  processing framework

- Abelian Monoid :: A monoid where the commutative property holds for
                    the binary operator.
  - Commutativity can be useful since it lets intermediate results be
    computed in any order

* 15.8 Laws

Why we should care about mathematical laws:
  - "proofs are programs and programs are proofs"
  - Gives us predictable composition of programs

Here are our monad laws (remember ~<>~ is an alias for ~mappend~):

#+BEGIN_SRC haskell
let
  left_identity  = mempty <> x == x
  right_identity = x <> mempty == x
  associativity  = x <> (y <> z) == (x <> y) <> z
  mconcat        = foldr (<>) mempty
#+END_SRC

In the above, the laws state that all the inequalities should evaluate
to true.

The important point is that you have these guarantees even if you
don't know what Monoid you'll be working with.

* 15.9 Different instance, same representation

Sometimes monoidal opperations are less about combining the values and
more about finding a summary value for a set.

** Examples
*** Bool
~Bool~ has two monoidal interfaces:

1. The ~All~ newtype for conjuntion (~&&~, ~True~)
2. The ~Any~ newtype for disjunction (~||~, ~False~)

*** Maybe

~Maybe~ can have more than two different ~Monoids~.

- The most obvious can be represented by ~First~ and ~Last~.
  - like boolean disjunction
  - But, they have extra info beyond true/false
    - Which ~Just~ value do we choose to return if we see one (first
      or last)

* 15.10 Reusing algebras by asking for algebras

We're going to write a monoid for ~Maybe~ other than ~First~ or
~Last~. This is going to be based off the fact that we can require the
type parameter to have an instance of ~Monoid~ itself.

See exercises.org "Optional Monoid" for my solution.

** Associativity

remember associativity means:

    : (a + b) + c == a + (b + c)

Whereas commutativity means:

    : a + b == b + a

So, for example ~(++)~ is associative, but not commutative, ~(+)~ is
both, and ~(-)~ is neither.

We can prove that ~(++)~ does not commute:

    : ghci> (++) [1,2,3] [4,5,6] == (flip (++)) [1,2,3] [4,5,6]
    : False

*Remember*: A monoid's binary operator is only required to obey
associativity, and may or may not obey commutativity.

** Identity

- An identity value turns the binary operation into the identity
  function
- It doesn't make sense to talk about, e.g. ~0~ and ~1~ as "identity
  values" outside of the context of the ~+~ and ~*~ operators.

This is the other law for Monoid. The binary operator must be
associative and have an identity value.

** Orphan instances

- orphan instance :: When an instance is defined for a (datatype,
     typeclass) pair, but not in the same file as either the datatype
     or typeclass.
  - That is, you don't own/control the typeclass or the datatype

Avoid orphan instances at all costs:

- If you own the datatype, put the typeclass instance with it.
- If you own the typeclass, put the data declarations with it.
- Otherwise, if you don't control either, you should use ~newtype~ to
  make a type you do control!

You really don't want anyone to be able to import types without their
typeclass instances or vice versa.

* 15.11 Madness

We're going to be making a mad-libs thing.

#+BEGIN_SRC haskell
import Data.Monoid

type Verb = String
type Adjective = String
type Adverb = String
type Noun = String
type Exclamation = String

madlibbin' :: Exclamation -> Adverb -> Noun -> Adjective -> String
madlibbin' e adv noun adj =
  e <> "! he said " <>
  adv <> " as he jumped into his car " <>
  noun <> " and drove off with his " <>
  adj <> " wife"
#+END_SRC

We can refactor this to use mconcat

#+BEGIN_SRC haskell
madlibbin2' :: Exclamation -> Adverb -> Noun -> Adjective -> String
madlibbin2' e adv noun adj =
  mconcat [
    e, "! he said ",
    adv, " as he jumped into his car ",
    noun, " and drove off with his ",
    adj, " wife"
  ]
#+END_SRC

* 15.12 Better living through QuickCheck

QuickCheck can provide us a way to quickly check if laws are being
obeyed.

** Validating associativity with QuickCheck

Given we want to test the propery of associativity, we could write
down a function that given a function, and some arguments, returns a
boolean when associativity holds for that function argument combo:

#+BEGIN_SRC haskell
-- asc, i.e. associativity
asc :: Eq a => (a -> a -> a) -> a -> a -> a -> Bool
asc (<>) a b c = a <> (b <> c) == (a <> b) <> c
#+END_SRC

But how do we quickcheck it?

#+BEGIN_SRC haskell
import Data.Monoid
import Test.QuickCheck

monoidAssoc :: (Eq m, Monoid m) => m -> m -> m -> Bool
monoidAssoc a b c = (a <> (b <> c)) == ((a <> b) <> c)
#+END_SRC

Then we can test this

    : quickCheck (monoidAssoc :: String -> String -> String -> Bool)

You can also use ~verboseCheck~ to see all the values that were tested.

** QuickChecking left and right identity

#+BEGIN_SRC haskell
monoidLeftIdentity :: (Eq m, Monoid m) => m -> Bool
monoidLeftIdentity a = (mempty <> a) == a

monoidRightIdentity :: (Eq m, Monoid m) => m -> Bool
monoidRightIdentity a = (a <> mempty) == a
#+END_SRC

    : quickCheck (monoidLeftIdentity :: String -> Bool)
    : quickCheck (monoidRightIdentity :: String -> Bool)

** Let's make quickcheck fail

We're going to try to implement ~Monoid~ for a Bool-like type, with
~mempty~ as false, and mappend as always false.

see ~./bullMappend.hs~ for the source

Why does it fail? The identities don't follow the laws!

* 15.13 Semigroup

- Semigroup :: A binar associative function

- A semigroup is a moniod without an identity.

Our definition of semigroup is:

#+BEGIN_SRC haskell
class Semigroup a where
  (<>) :: a -> a -> a
#+END_SRC

Which should obey the following law:

    : (a <> b) <> c == a <> (b <> c)

** NonEmpty

We can create a useful datatype, which is a non-empty list type. It
must always have at least one value. It's defined:

#+BEGIN_SRC haskell
data NonEmpty a = a :| [a]
  deriving (Eq, Ord, Show)
#+END_SRC

*Remember:* Data constructors with onl non-alphanumeric symbols that
begin with a colon are infix by default.

Interestingly we can't write a Monoid for ~NonEmpty~ because it has no
identity value, by design (I question this, see ~../questions.org~).

** Strength can be a weakness

- strenth of an algebra :: The number of operations it provides, and
     therefore the number of things you can do with an instance of the
     algebra, without knowing more about the underlying type.

For example, ~Monoid~ is stronger than ~Semigroup~.

Sometimes we can't have stronger algebras for useful datatypes though.

What is weaker than semigroup? A magma.

- magma :: A binary operator (not necessarily associative).

* 15.14 Chapter exercises

See exercises.org

* 15.15 Definitions

chapter end pg 611
